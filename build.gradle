plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.freefair.lombok' version '5.3.0'
    id 'com.jfrog.bintray' version '1.8.5'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = '0.1.0-SNAPSHOT'
group = 'com.kneelawk'

repositories {
    jcenter()
}

dependencies {
    api 'com.google.guava:guava:21.0'
    implementation 'org.jetbrains:annotations:19.0.0'
    implementation 'commons-io:commons-io:2.8.0'
}

processResources {
    inputs.property "version", project.version

    filesMatching("**/marionette-rt.properties") {
        expand "marionette_version": project.version
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        marionetteRt(MavenPublication) {
            from components.java
            pom {
                name = 'Marionette Runtime Library'
                description = 'The library used during Marionette Minecraft integration tests'
                url = 'https://github.com/Kneelawk/marionette-rt'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'kneelawk'
                        name = 'Jed Pommert'
                        email = 'kneelawk@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Kneelawk/marionette-rt.git'
                    developerConnection = 'scm:git:ssh://github.com:Kneelawk/marionette-rt.git'
                    url = 'https://github.com/Kneelawk/marionette-rt/tree/main'
                }
            }
        }
    }
}

if (System.getenv('BINTRAY_KEY') != null && System.getenv('BINTRAY_USER') != null) {
    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publications = ['marionetteRt']

        pkg {
            repo = 'maven'
            name = 'marionette-rt'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/Kneelawk/marionette-rt.git'
            version {
                name = project.version
                released = new Date()
            }
        }
    }
}
